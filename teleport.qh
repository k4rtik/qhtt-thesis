teleport : q: qbit -> QST b: qbit
                          {ψ: vector}
                          (requires {q =q ψ})
                          (ensures  {class(q) ∧ b =q ψ})
teleport = \q.do
              (a, b) <- bell00 ()
              apply CX to (q, a)
              apply H to q
              x <- meas q
              y <- meas a
              apply CX to (y, b)
              apply CZ to (x, b)
              return b

teleport : q: qbit -> QST b: qbit
                          {ψ: vector}
                          (requires {q =q ψ})
                          (ensures  {class(q) ∧ b =q ψ})
teleport = \q.do
              -- q =q ψ
              -- ⇒ q =q α|0⟩+β|1⟩
              (a, b) <- bell00 ()
              -- q =q ψ ∧ (a, b) =q |β00⟩
              -- ⇒ (q, a, b) =q 1/√2(α|0⟩(|00⟩+|11⟩)+β|1⟩(|00⟩+|11⟩))
              apply CX to (q, a)
              -- (CX on (q, a)) . (q =q ψ ∧ (a, b) =q |β00⟩)
              -- ⇒ (q, a, b) =q 1/√2(α|0⟩(|00⟩+|11⟩)+β|1⟩(|10⟩+|01⟩))
              apply H to q
              -- (H on q) . ((q, a, b) =q 1/√2(α|0⟩(|00⟩+|11⟩)+β|1⟩(|10⟩+|01⟩)))
              -- ⇒ (q, a, b) =q 1/2(α(|0⟩+|1⟩)(|00⟩+|11⟩) + β(|0⟩-|1⟩)(|10⟩+|01⟩))
              -- ⇒ (q, a, b) =q 1/2(|00⟩(α|0⟩+β|1⟩) + |01⟩(α|1⟩+β|0⟩)
              --                   + |10⟩(α|0⟩-β|1⟩) + |11⟩(α|1⟩-β|0⟩))
              x <- meas q
              -- class(q) ∧
              -- (e_x, a, b) =q 1/2(|00⟩(α|0⟩+β|1⟩) + |01⟩(α|1⟩+β|0⟩)
              --                 + |10⟩(α|0⟩-β|1⟩) + |11⟩(α|1⟩-β|0⟩))
              y <- meas a
              -- class(q) ∧ class(a) ∧
              -- (e_x, e_y, b) =q 1/2(|00⟩(α|0⟩+β|1⟩) + |01⟩(α|1⟩+β|0⟩)
              --                  + |10⟩(α|0⟩-β|1⟩) + |11⟩(α|1⟩-β|0⟩))
              apply CX to (y, b)
              -- class(q) ∧ class(a) ∧
              -- (e_x, e_y, b) =q 1/2(|00⟩(α|0⟩+β|1⟩) + |01⟩(α|0⟩+β|1⟩)
              --                  + |10⟩(α|0⟩-β|1⟩) + |11⟩(α|0⟩-β|1⟩))
              apply CZ to (x, b)
              -- class(q) ∧ class(a) ∧
              -- (e_x, e_y, b) =q 1/2(|00⟩(α|0⟩+β|1⟩) + |01⟩(α|0⟩+β|1⟩)
              --                  + |10⟩(α|0⟩+β|1⟩) + |11⟩(α|0⟩+β|1⟩))
              -- ⇒ (e_x, e_y) =q 1/2(|00⟩+|01⟩+|10⟩+|11⟩) ∧ b =q α|0⟩+β|1⟩
              return b
              -- class(q) ∧ b =q α|0⟩+β|1⟩
              -- ⇒ class(q) ∧ b =q ψ
